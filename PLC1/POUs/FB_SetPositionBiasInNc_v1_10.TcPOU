<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="FB_SetPositionBiasInNc_v1_10" Id="{19676e9a-f0b5-43f4-9d3b-5ee8a410d7e5}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_SetPositionBiasInNc_v1_10
VAR_INPUT
	bInit: BOOL;
	bExecute: BOOL;
	fOffset: LREAL;
END_VAR
VAR_OUTPUT
	bBusy: BOOL;
	bDone: BOOL;
	bError: BOOL;
	nErrorId: UDINT;
END_VAR
VAR_IN_OUT
	fPositionBias: LREAL;
	Axis: TC2_MC2.AXIS_REF;
END_VAR
VAR
	nState: UINT;
	fbADSREAD: ADSREAD;
	fbADSWRITE: ADSWRITE;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[

(*Sequence to set Position Bias in Nc*)
CASE nState OF
0: (*Start sequence. Wait until bExecute is TRUE*)
	IF bInit THEN
	bBusy:=TRUE;
	bError:=FALSE;
	nErrorId:=0;
	nState:=30;
	ELSIF bExecute THEN
	bBusy:=TRUE;
	bError:=FALSE;
	nErrorId:=0;
	nState:=10;
	END_IF



10: (*Read actual Position Bias in Nc*)
fbADSREAD(
PORT:=500,
IDXGRP:=16#5000+Axis.NcToPlc.AxisId,
IDXOFFS:=16#7,
LEN:=SIZEOF(fPositionBias),
DESTADDR:=ADR(fPositionBias),
READ:=TRUE);



(*Wait until it's done or if an error occurs*)
IF NOT fbADSREAD.ERR THEN
IF NOT fbADSREAD.BUSY THEN
fbADSREAD(READ:=FALSE);
nState:=20;
END_IF
ELSE
nErrorId:=fbADSREAD.ERRID;
nState:=999;
END_IF



20:(*Calculate new Position Bias*)
fPositionBias:=fPositionBias+((Axis.NcToPlc.ActPos-REAL_TO_LREAL(fOffset)) *-1);
nState:=30;



30: (*Write Position Bias in Nc*)
fbADSWRITE(
PORT:=500,
IDXGRP:=16#5000+Axis.NcToPlc.AxisId,
IDXOFFS:=16#7,
LEN:=SIZEOF(fPositionBias),
SRCADDR:=ADR(fPositionBias),
WRITE:=TRUE);



(*Wait until it's done or if an error occurs*)
IF NOT fbADSWRITE.ERR THEN
IF NOT fbADSWRITE.BUSY THEN
fbADSWRITE(WRITE:=FALSE);
nState:=40;
END_IF
ELSE
nErrorId:=fbADSWRITE.ERRID;
nState:=999;
END_IF



40: (*Sequense is done. Waits until bExecute is FALSE*)
bBusy:=FALSE;
bDone:=TRUE;
IF NOT (bInit OR bExecute) THEN
bDone:=FALSE;
nState:=0;
END_IF



999: (*Error in sequence*)
bError:=TRUE;
bBusy:=FALSE;
bDone:=FALSE;
fbADSREAD(READ:=FALSE);
fbADSWRITE(WRITE:=FALSE);
IF NOT (bInit OR bExecute) THEN
nState:=0;
END_IF



END_CASE]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>